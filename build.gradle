plugins {
    id "io.freefair.lombok" version "3.2.0"
}

group 'au.com.skytix'
version '1.0.13'

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'signing'


repositories {
    mavenCentral()
    maven {
        url "https://plugins.gradle.org/m2/"
    }
    
}

task sourceJar(type: Jar) {
    classifier "sources"
    from sourceSets.main.allJava
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier "javadoc"
    from javadoc.destinationDir
}

artifacts {
    archives jar
    archives sourceJar
    archives javadocJar
}

dependencies {
    compile group: 'org.apache.mesos', name: 'mesos', version: '1.11.0'
    compile group: 'org.apache.curator', name: 'curator-framework', version: '5.2.0'
    compile group: 'org.apache.zookeeper', name: 'zookeeper', version: '3.7.0'
    compile group: 'com.google.guava', name: 'guava', version: '30.1.1-jre'
    compile group: 'org.apache.commons', name: 'commons-lang3', version: '3.12.0'
    compile group: 'ch.qos.logback', name: 'logback-classic', version: '1.2.6'
    testCompile group: 'junit', name: 'junit', version: '4.12'
    compile group: 'com.googlecode.protobuf-java-format', name: 'protobuf-java-format', version: '1.4'
    compile group: 'javax.activation', name: 'activation', version: '1.1.1'
    compile group: 'javax.xml.bind', name: 'jaxb-api', version: '2.4.0-b180830.0359'

}

signing {
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: sonatypeUsername, password: sonatypePassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: sonatypeUsername, password: sonatypePassword)
            }

            pom.project {
                name 'mesos-scheduler-client'
                packaging 'jar'
                // optionally artifactId can be defined here
                description 'Mesos HTTP Scheduler'
                url 'https://github.com/skytix-dev/mesos-scheduler-client'

                scm {
                    connection 'scm:git:git://github.com/skytix-dev/mesos-scheduler-client.git'
                    developerConnection 'scm:git:ssh://git@github.com:skytix-dev/mesos-scheduler-client.git'
                    url 'https://github.com/skytix-dev/mesos-scheduler-client'
                }

                licenses {
                    license {
                        name 'The Apache License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        name 'Marc Dergacz'
                        email 'marc@skytix.com.au'
                    }
                }
            }
        }
    }
}